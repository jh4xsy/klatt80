C       COEWAV.FOR              D.H. KLATT              8/1/78
C
C       "COEF-TO-WAVE" TRANSFORMATION SUBROUTINE
C               (FOR A 16-BIT PDP-11 COMPUTER)
C
C       SIMULATION OF THE HARDWARE KLATT SYNTHESIZER
C       TAKE 50 COEFIECIENTS FROM COMMON ARRAY C, AND
C       SYNTHESIZE NEXT NNXWS SAMPLES OF THE OUTPUT WAVEFORM
C
        SUBROUTINE COEWAV(IWAVE,OUTMA)
C               IWAVE IS AN ARRAY IN WHICH WAVEFORM SAMPLES ARE PLACED
C                 LEFT-JUSTIFIED IN A 36-BIT WORD
C               OUTMA IS RETURN ARG INDICATING MAX ABSOL. VALUE OF WAVE
C                 IF CALLING PROGRAM SETS TO -1., COEWAV IS INITIALIZED
C
        REAL NOISE,INPUTS,INPUT,IMPULS
        DIMENSION IWAVE(1),C(50)
        COMMON /COEFS/ C
C     COEFICIENT VALUES IN C(50) ARE REAL
        EQUIVALENCE (C(1),IMPULS),(C(2),SINAMP),(C(3),AFRICI)
        EQUIVALENCE (C(4),AASPI),(C(5),A1PAR),(C(6),A2PAR)
        EQUIVALENCE (C(7),A3PAR),(C(8),A4PAR),(C(9),A5PAR)
        EQUIVALENCE (C(10),A6PAR),(C(11),ABPAR),(C(12),ANPAR)
        EQUIVALENCE (C(13),AGP),(C(14),BGP),(C(15),CGP)
        EQUIVALENCE (C(16),AGZ),(C(17),BGZ),(C(18),CGZ)
        EQUIVALENCE (C(19),AGS),(C(20),BGS),(C(21),CGS)
        EQUIVALENCE (C(22),A1),(C(23),B1),(C(24),C1)
        EQUIVALENCE (C(25),A2),(C(26),B2),(C(27),C2)
        EQUIVALENCE (C(28),A3),(C(29),B3),(C(30),C3)
        EQUIVALENCE (C(31),A4),(C(32),B4),(C(33),C4)
        EQUIVALENCE (C(34),A5),(C(35),B5),(C(36),C5)
        EQUIVALENCE (C(37),A6),(C(38),B6),(C(39),C6)
        EQUIVALENCE (C(40),ANP),(C(41),BNP),(C(42),CNP)
        EQUIVALENCE (C(43),ANZ),(C(44),BNZ),(C(45),CNZ)
        EQUIVALENCE (C(46),PLSTEP)
C     MAXIMUM VALUE FOR A WAVEFORM SAMPLE (LEFT-JUSTIFY IN 36-BIT WORD)
        DATA WAVMA,WAVMAX/32767,-32767/
C
C     INITIALIZE COEWAV IF OUTMA=-1.
C     ZERO MEMORY REGISTERS IN ALL RESONATORS
        IF (OUTMA.GE.0) GO TO 250
249     YL11P=0.
        YL12P=0.
        YL21P=0.
        YL22P=0.
        YL31P=0.
        YL32P=0.
        YL41P=0
        YL42P=0.
        YL51P=0.
        YL52P=0.
        YL61P=0.
        YL62P=0.
        YLNP1=0.
        YLNP2=0.
        YL11C=0.
        YL12C=0.
        YL21C=0.
        YL22C=0.
        YL31C=0.
        YL32C=0.
        YL41C=0.
        YL42C=0.
        YL51C=0.
        YL52C=0.
        YL61C=0.
        YL62C=0.
        YLNP1C=0.
        YLNP2C=0.
        YLNZ1C=0.
        YLNZ2C=0.
        YLGP1=0.
        YLGP2=0.
        YLGS1=0.
        YLGS2=0.
        YLGS3=0.
        YLGS4=0.
        YLGZ1=0.
        YLGZ2=0.
C     ZERO ALL OTHER MEMORY REGISTERS 
        NPULSE=1
        MPULSE=0
        UGLOTX=0.
        UGLOTL=0.
        OUTMA=0.
        AFRIC=0.
        STEP=0.
        AASPIR=0.
C
C     GENERATE NNXWS SAMPLES OF OUTPUT WAVEFORM
250     CONTINUE
C     TRANSLATE SOME COEFICIENTS TO INTEGER
        NPULSN=C(47)
        NNXWS=C(48)
        NXSW=C(49)
        NNXFC=C(50)
        XNSAMI=1.0/FLOAT(NNXWS)
C     DELTA AMPLITUDE OF ASPIRATION
        DAHH=(AASPI-AASPIR)*XNSAMI
C     DELTA AMPLITUDE OF FRICATION
        DAFF=(AFRICI-AFRIC)*XNSAMI
C
C   MAIN LOOP
        DO 530 NTIME=1,NNXWS
C     GENERATE NEW GLOTTAL PULSE IF PERIOD COUNTER EXCEEDED
        NPULSE=NPULSE-1
        IF (NPULSE.GT.0) GO TO 260
C     AND IF NPULSN.GT.1 (I.E. IF F0>0 AND AV+AVS>0)
        IF (NPULSN.LE.1) GO TO 260
C     RESET PULSE COUNTER
        NPULSE=NPULSN
C     PULSE COUNTER FOR MODULATED NOISE
        MPULSE=NPULSE/2
C     SET AMPLITUDE OF NORMAL VOICING IMPULSE
        INPUT=IMPULS
C     AMPLITUDE OF QUASI-SINUSIODAL VOICING
        INPUTS=SINAMP
        GO TO 275
C     SET INPUT TO ZERO BETWEEN GLOTTAL IMPULSES
260     INPUT=0.
        INPUTS=0.
C     RESONATOR RGP:
275     YGP=AGP*INPUT + BGP*YLGP1 + CGP*YLGP2
        YLGP2=YLGP1
        YLGP1=YGP
C     GLOTTAL ZERO PAIR RGZ:
290     YGZ=AGZ*YGP + BGZ*YLGZ1 + CGZ*YLGZ2
        YLGZ2=YLGZ1
        YLGZ1=YGP
C     QUASI-SINUSOIDAL VOICING PRODUCED BY IMPULSE INTO RGP AND RGS:
        YGS=INPUTS*AGS + BGS*YLGS1 + CGS*YLGS2
        YLGS2=YLGS1
        YLGS1=YGS
        YGS=AGP*YGS + BGP*YLGS3 + CGP*YLGS4
        YLGS4=YLGS3
        YLGS3=YGS
C     GLOTTAL VOLUME VELOCITY IS THE SUM OF NORMAL AND
C     QUASI-SINUSOIDAL VOICING
        UGLOT2=YGZ + YGS
C     RADIATION CHARACTERISTIC IS A ZERO AT THE ORIGIN
        UGLOT=UGLOT2-UGLOTX
        UGLOTX=UGLOT2
C
C   TURBULENCE NOISE OF ASPIRATION AND FRICATION
C     GENERATE RANDOM NOISE, RANDOM PRODUCES UNIFORM DIST. (0. TO 1.)
370     NOISE=0.
C     MAKE PSEUDO-GAUSSIAN
        DO 371 NRANDX=1,16
371     NOISE=NOISE+RAN(IRAN1,IRAN2)
C     SUBTRACT OFF DC
        NOISE=NOISE-8.
C     MODULATE NOISE DURING SECOND HALF OF A GLOTTAL PERIOD
375     IF (MPULSE.LE.0) NOISE=NOISE/2.
        MPULSE=MPULSE-1
C     LOW-PASS NOISE AT -6 DB/OCTAVE TO SIMULATE SOURCE IMPEDANCE
C     HIGH-PASS NOISE AT +6 DB/OCTAVE FOR RADIATION CHARACTERISTIC
C          (TWO EFFECTS CANCEL ONE ANOTHER)
C     GLOTTAL SOURCE VOLUME VELOCITY = VOICING+ASPIRATION
        AASPIR=AASPIR+DAHH
        UASP=AASPIR*NOISE 
380     UGLOT=UGLOT+UASP
C     SET FRICATION SOURCE VOLUME VELOCETY
390     AFRIC=AFRIC+DAFF
C     PREPARE TO ADD IN A STEP EXCITATION OF VOCAL TRACT
C     IF PLOSIVE RELEASE (I.E. IF PLSTEP.GT.0.)
        IF (PLSTEP.LE.0.) GO TO 391
        STEP=-PLSTEP
        PLSTEP=0.
391     UFRIC=AFRIC*NOISE
C
C   SEND GLOTTAL SOURCE THRU CASCADE VOCAL TRACT RESONATORS
C     DO FORMANT EQUATIONS FOR NNXFC FORMANTS IN DESCENDING ORDER
C     TO MINIMIZE TRANSCIENTS
        IF (NXSW.EQ.1) GO TO 430
C     BYPASS R6 IF NNXFC LESS THAN 6
        Y6C=UGLOT
        IF (NNXFC.LT.6) GO TO 415
        Y6C=A6*UGLOT + B6*YL61C + C6*YL62C
        YL62C=YL61C
        YL61C=Y6C
C     BYPASS R5 IF NNXFC LESS THAN 5
415     Y5C=Y6C
        IF (NNXFC.LT.5) GO TO 416
        Y5C=A5*Y6C + B5*YL51C + C5*YL52C
        YL52C=YL51C
        YL51C=Y5C
416     Y4C=A4*Y5C + B4*YL41C + C4*YL42C
        YL42C=YL41C
        YL41C=Y4C
        Y3C=A3*Y4C + B3*YL31C + C3*YL32C
        YL32C=YL31C
        YL31C=Y3C
        Y2C=A2*Y3C + B2*YL21C + C2*YL22C
        YL22C=YL21C
        YL21C=Y2C
        Y1C=A1*Y2C + B1*YL11C + C1*YL12C
        YL12C=YL11C
        YL11C=Y1C
C     NASAL ZERO-PAIR RNZ:
420     YZC=ANZ*Y1C + BNZ*YLNZ1C + CNZ*YLNZ2C
        YLNZ2C=YLNZ1C
        YLNZ1C=Y1C
C     NASAL RESONATOR RNP:
        YPC=ANP*YZC + BNP*YLNP1C + CNP*YLNP2C
        YLNP2C=YLNP1C
        YLNP1C=YPC
        ULIPSV=YPC
C     ZERO OUT VOICING INPUT TO PARALEL BRANCH
C     IF CASCADE BRANCH HAS BEEN USED
425     UGLOT=0.
        UGLOTL=0.
C
C   SEND VOICING AND FRICATION NOISE THRU PARALLEL RESONATORS
C     INCREMENT RESONATOR AMPLITUDES GRADUALLY
430     CONTINUE
C     FIRST PARALLEL FORMANT R1' (EXCITED BY VOICING ONLY)
        Y1P=A1*A1PAR*UGLOT + B1*YL11P + C1*YL12P
        YL12P=YL11P
        YL11P=Y1P
C     NASAL POLE RN' (EXCITED BY FIRST DIFF. OF VOICING SOURCE)
        UGLOT1=UGLOT-UGLOTL
        UGLOTL=UGLOT
        IF (NXSW.NE.1) UGLOT1=0.
        YN=ANP*ANPAR*UGLOT1 + BNP*YLNP1 + CNP*YLNP2
        YLNP2=YLNP1
        YLNP1=YN
C     EXCITE FORMANTS R2'-R4' WITH FRIC NOISE PLUS FIRST-DIFF. VOICING
        Y2P=A2*A2PAR*(UFRIC+UGLOT1) + B2*YL21P +C2*YL22P
        YL22P=YL21P
        YL21P=Y2P
        Y3P=A3*A3PAR*(UFRIC+UGLOT1) + B3*YL31P +C3*YL32P
        YL32P=YL31P
        YL31P=Y3P
        Y4P=A4*A4PAR*(UFRIC+UGLOT1) + B4*YL41P +C4*YL42P
        YL42P=YL41P
        YL41P=Y4P
C     EXCITE FORMANT RESONATORS R5'-R6' WITH FRIC NOISE
        Y5P=A5*A5PAR*UFRIC + B5*YL51P +C5*YL52P
        YL52P=YL51P
        YL51P=Y5P
        Y6P=A6*A6PAR*UFRIC + B6*YL61P +C6*YL62P
        YL62P=YL61P
        YL61P=Y6P
C     ADD UP OUTPUTS FROM RN', R1' - R6' AND BYPASS PATH
        ULIPSF=Y1P-Y2P+Y3P-Y4P+Y5P-Y6P+YN-ABPAR*UFRIC
440     CONTINUE
C     ADD CASCADE AND PARALLEL VOCAL TRACT OUTPUTS
C     (SCALE BY 170 TO LEFT JUSTIFY IN 16-BIT WORD)
450     ULIPS=(ULIPSV+ULIPSF+STEP)*(170.)
        STEP=.995*STEP
C     FIND CUMULATIVE ABSOL. MAX. OF WAVEFORM SINCE BEGINNING OF UTT.
500     IF (ULIPS.GT.OUTMA) OUTMA=ULIPS
        IF (-ULIPS.GT.OUTMA) OUTMA=-ULIPS
C     TRUNCATE WAVEFORM SAMPLES TO ABS[WAVMA]
        IF (ULIPS.LE.WAVMA) GO TO 510
        ULIPS=WAVMA
510     IF (ULIPS.GE.WAVMAX) GO TO 520
        ULIPS=WAVMAX
520     IWAVE(NTIME)=ULIPS
530     CONTINUE
540     RETURN
        END
